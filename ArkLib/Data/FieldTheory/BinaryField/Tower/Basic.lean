/-
Copyright (c) 2024-2025 ArkLib Contributors. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors : Quang Dao, Chung Thai Nguyen
-/

import ArkLib.Data.FieldTheory.BinaryField.Tower.Prelude
import ArkLib.Data.RingTheory.AlgebraTower
import Mathlib.Tactic.DepRewrite

/-!
# Binary Tower Fields

Define the binary tower field GF(2^{2^k}) as an iterated quadratic extension of GF(2).

## Main Definitions

- `BTField k` : the binary tower field GF(2^{2^k}) as an iterated quadratic extension of GF(2),
  where `BTField 0 = GF(2)`

## References

* [Wiedemann, D., *An Iterated Quadratic Extension of GF(2)*][Wie88]
* [Fan, J.L. and Paar, C., *On efficient inversion in tower fields of characteristic two*][FP97]
* [Lin, S., Chung, W., and Han, Y.S., *Novel polynomial basis and its application to Reed-Solomon
    erasure codes*][LCH14]
* [Diamond, B.E. and Posen, J., *Succinct Arguments over Towers of Binary Fields*][DP23]
* [Diamond, B.E. and Posen, J., *Polylogarithmic proofs for multilinears over binary towers*][DP24]

## TODOs

-/

namespace BinaryTower
noncomputable section

open Polynomial AdjoinRoot Module

section BTFieldDefs

structure BinaryTowerResult (F : Type _) (k : ‚Ñï) where
  vec : (List.Vector F (k + 1))
  instField : (Field F)
  instFintype : Fintype F
  specialElement : F
  specialElementNeZero : NeZero specialElement
  firstElementOfVecIsSpecialElement [Inhabited F] : vec.1.headI = specialElement
  instIrreduciblePoly : (Irreducible (p := (definingPoly specialElement)))
  sumZeroIffEq : ‚àÄ (x y : F), x + y = 0 ‚Üî x = y
  fieldFintypeCard : Fintype.card F = 2^(2^k)
  traceMapEvalAtRootsIs1 : TraceMapProperty F specialElement k

structure BinaryTowerInductiveStepResult (k : ‚Ñï) (prevBTField : Type _)
  (prevBTResult : BinaryTowerResult prevBTField k) [instPrevBTFieldIsField : Field prevBTField]
  (prevPoly : Polynomial prevBTField) (F : Type _) where
  binaryTowerResult : BinaryTowerResult F (k+1)
  eq_adjoin : F = AdjoinRoot prevPoly
  u_is_root : Eq.mp (eq_adjoin) binaryTowerResult.specialElement = AdjoinRoot.root prevPoly
  eval_defining_poly_at_root : Eq.mp (eq_adjoin) binaryTowerResult.specialElement^2 +
    Eq.mp (eq_adjoin) binaryTowerResult.specialElement * (of prevPoly) prevBTResult.specialElement
    + 1 = 0

set_option maxHeartbeats 1000000 in
-- it takes more heartbeats to prove this theorem
def binary_tower_inductive_step
  (k : Nat)
  (prevBTField : Type _) [Field prevBTField]
  (prevBTResult : BinaryTowerResult prevBTField k)
: Œ£' (F : Type _), BinaryTowerInductiveStepResult (k:=k) (prevBTField:=prevBTField)
  (prevBTResult:=prevBTResult) (prevPoly:=definingPoly (F:=prevBTField)
    (instField:=prevBTResult.instField) (s:=prevBTResult.specialElement)) (F:=F)
  (instPrevBTFieldIsField:=prevBTResult.instField) := by
  letI := prevBTResult.instField
  letI := prevBTResult.instFintype
  let elts := prevBTResult.vec
  set t1 : prevBTField := prevBTResult.specialElement
  letI inst_t1_NeZero : NeZero t1 := prevBTResult.specialElementNeZero
  have t1_ne_zero_in_prevBTField : t1 ‚â† 0 := inst_t1_NeZero.out
  set prevPoly := definingPoly t1
  let prevPolyDegIs2 : prevPoly.degree = 2 := degree_definingPoly t1
  let prevPolyNatDegIs2 := natDegree_definingPoly t1
  let prevPolyIsMonic : (Monic prevPoly) := definingPoly_is_monic t1
  let prevPolyNeZero := prevPolyIsMonic.ne_zero
  have degPrevPolyNe0 : prevPoly.degree ‚â† 0 := by
    intro h_deg_eq_0
    rw [prevPolyDegIs2] at h_deg_eq_0
    contradiction
  let instPrevPolyIrreducible := prevBTResult.instIrreduciblePoly
  let prevBTFieldCard : Fintype.card prevBTField = 2^(2^k) := prevBTResult.fieldFintypeCard
  let instFactIrrPoly : Fact (Irreducible prevPoly) :=
    ‚ü®instPrevPolyIrreducible‚ü© -- used for AdjoinRoot.instField
  let sumZeroIffEqPrevBTField : ‚àÄ (x y : prevBTField), x + y = (0 : prevBTField)
    ‚Üî x = y := by exact prevBTResult.sumZeroIffEq
  let curBTField := AdjoinRoot prevPoly -- BTF(k) ‚âà GF(2^(2^(k+1)))
  let instFieldAdjoinRootOfPoly : Field curBTField := by
    exact AdjoinRoot.instField (f := prevPoly)
  -- Lift to new BTField level
  let u : curBTField := AdjoinRoot.root prevPoly -- adjoined root and generator of curBTField
  let adjoinRootOfPoly : AdjoinRoot prevPoly = curBTField := by
    simp only [curBTField]
  have u_is_inv_of_u1 : u = u‚Åª¬π‚Åª¬π := (inv_inv u).symm
  let newElts := elts.map (fun x => (AdjoinRoot.of prevPoly).toFun x)

  have unique_linear_form_of_elements_in_curBTField : ‚àÄ (c1 : AdjoinRoot prevPoly),
    ‚àÉ! (p : prevBTField √ó prevBTField), c1 = (of prevPoly) p.1 * root prevPoly + (of prevPoly) p.2
      := unique_linear_form_of_elements_in_adjoined_commring
        (hf_deg := prevPolyNatDegIs2) (hf_monic := prevPolyIsMonic)

  have selfSumEqZero : ‚àÄ (x : curBTField), x + x = 0 := self_sum_eq_zero
    (sumZeroIffEqPrevBTField) (prevPoly) (prevPolyNatDegIs2) (prevPolyIsMonic)

  have sumZeroIffEq : ‚àÄ (x y : curBTField), x + y = 0 ‚Üî x = y :=
    sum_zero_iff_eq_of_self_sum_zero (selfSumEqZero)

  have u_is_root : u = AdjoinRoot.root prevPoly := rfl
  have h_eval : ‚àÄ (x : curBTField), eval‚ÇÇ (of prevPoly) x (X^2 + (C t1 * X + 1)) =
    x^2 + (of prevPoly) t1 * x + 1 := eval‚ÇÇ_quadratic_prevField_coeff (of_prev := of prevPoly) t1

  have eval_prevPoly_at_root : u^2 + (of prevPoly) t1 * u + 1 = 0 := by -- u^2 + t1 * u + 1 = 0
      have h_root : eval‚ÇÇ (of prevPoly) u prevPoly = 0 := by
        rw [u_is_root]
        exact eval‚ÇÇ_root prevPoly
      have h_expand : eval‚ÇÇ (of prevPoly) u (X^2 + (C t1 * X + 1)) = 0 := by
        rw [‚Üêadd_assoc]
        change eval‚ÇÇ (of prevPoly) u (definingPoly (F:=prevBTField) (s:=t1)) = 0
        exact h_root
      rw [h_eval u] at h_expand
      exact h_expand
  have h_u_square : u^2 = u*t1 + 1 := by
    have h1 := eval_prevPoly_at_root
    rw [‚Üêadd_right_inj (u^2), ‚Üêadd_assoc, ‚Üêadd_assoc] at h1
    rw [selfSumEqZero (u^2), zero_add, add_zero, mul_comm] at h1
    exact h1.symm
  have one_ne_zero : (1 : curBTField) ‚â† (0 : curBTField) := by exact NeZero.out
  have specialElementNeZero : u ‚â† 0 := by
    by_contra h_eq
    rw [h_eq] at eval_prevPoly_at_root
    have two_pos : 2 ‚â† 0 := by norm_num
    rw [zero_pow two_pos, mul_zero, zero_add, zero_add] at eval_prevPoly_at_root
    exact one_ne_zero eval_prevPoly_at_root
  letI inst_u_NeZero : NeZero u := { out := specialElementNeZero }
    -- Step 2 : transform the equations in curBTField and create new value equalitiy bounds
    -- (1) c1 + c2 = (a + c) * u + (b + d) = u
    -- <=> u * (1 - a - c) = b + d
  have u_plus_u1_eq_t1 : u + u‚Åª¬π = t1 := sum_of_root_and_inverse_is_t1 (u:=u)
    (t1:=(of prevPoly) t1) (specialElementNeZero)
    (eval_prevPoly_at_root) (sumZeroIffEq)

  let f : curBTField ‚Üí prevBTField √ó prevBTField := fun c1 =>
    let h := unique_linear_form_of_elements_in_curBTField c1  -- Get the unique existential proof
    Classical.choose h

  have inj_f : Function.Injective f := by
    intros c1 c2 h_eq
    unfold f at h_eq
    -- h_eq is now (a1, b1) = (a2, b2), where a1, b1, a2, b2 are defined with Classical.choose
    let h1 := unique_linear_form_of_elements_in_curBTField c1
    let h2 := unique_linear_form_of_elements_in_curBTField c2
    let a1 := (Classical.choose h1).1
    let b1 := (Classical.choose h1).2
    let a2 := (Classical.choose h2).1
    let b2 := (Classical.choose h2).2
    -- Assert that h_eq matches the pair equality
    have pair_eq : (a1, b1) = (a2, b2) := h_eq
    have ha : a1 = a2 := (Prod.ext_iff.mp pair_eq).1
    have hb : b1 = b2 := (Prod.ext_iff.mp pair_eq).2
    have h1_eq : c1 = (of prevPoly) a1 * root prevPoly + (of prevPoly) b1 :=
      (Classical.choose_spec h1).1
    have h2_eq : c2 = (of prevPoly) a2 * root prevPoly + (of prevPoly) b2 :=
      (Classical.choose_spec h2).1
    rw [h1_eq, h2_eq, ha, hb]

  have surj_f : Function.Surjective f := by
    intro (p : prevBTField √ó prevBTField)
    let c1 := (of prevPoly) p.1 * root prevPoly + (of prevPoly) p.2
    use c1
    have h_ex : c1 = (of prevPoly) p.1 * root prevPoly + (of prevPoly) p.2 := rfl
    have h_uniq := unique_linear_form_of_elements_in_curBTField c1
    have p_spec : c1 = (of prevPoly) p.1 * root prevPoly + (of prevPoly) p.2 := h_ex
    -- Show that f c1 = p by using the uniqueness property
    have h_unique := (Classical.choose_spec h_uniq).2 p p_spec
    -- The function f chooses the unique representation, so f c1 must equal p
    exact h_unique.symm

  have bij_f : Function.Bijective f := by
    constructor
    ¬∑ exact inj_f  -- Injectivity from instFintype
    ¬∑ exact surj_f

  have equivRelation : curBTField ‚âÉ prevBTField √ó prevBTField := by
    exact Equiv.ofBijective (f := f) (hf := bij_f)

  let instFintype : Fintype curBTField := by
    exact Fintype.ofEquiv (prevBTField √ó prevBTField) equivRelation.symm

  let fieldFintypeCard : Fintype.card curBTField = 2^(2^(k + 1)) := by
    let e : curBTField ‚âÉ prevBTField √ó prevBTField := Equiv.ofBijective f bij_f
    -- ‚ä¢ Fintype.card curBTField = 2 ^ 2 ^ (k + 1)
    have equivCard := Fintype.ofEquiv_card equivRelation.symm
    rw [Fintype.card_prod] at equivCard
    rw [prevBTFieldCard] at equivCard -- equivCard : Fintype.card curBTField = 2 ^ 2 ^ k * 2 ^ 2 ^ k
    have card_simp : 2 ^ 2 ^ k * 2 ^ 2 ^ k = 2 ^ (2 ^ k + 2 ^ k) := by rw [Nat.pow_add]
    have exp_simp : 2 ^ k + 2 ^ k = 2 ^ (k + 1) := by
      rw [‚ÜêNat.mul_two, Nat.pow_succ]
    rw [card_simp, exp_simp] at equivCard
    exact equivCard

  let newPoly : curBTField[X] := definingPoly (F:=curBTField) (s:=u)

  have traceMapEvalAtRootsIs1 := traceMapProperty_of_quadratic_extension
    (F_prev:=prevBTField) (t1:=t1) (k:=k)
    (fintypeCardPrev:=prevBTResult.fieldFintypeCard)
    (F_cur:=curBTField) (u:=u) (h_rel:= {
      sum_inv_eq := u_plus_u1_eq_t1
      h_u_square := h_u_square
    }) (prev_trace_map := prevBTResult.traceMapEvalAtRootsIs1) (sumZeroIffEq := sumZeroIffEq)

  letI : CharP curBTField 2 := charP_eq_2_of_add_self_eq_zero (F:=curBTField)
    (sumZeroIffEq:=sumZeroIffEq)
  let instIrreduciblePoly : Irreducible newPoly :=
    irreducible_quadratic_defining_poly_of_traceMap_eq_1 (F:=curBTField) (s:=u) (k:=k+1)
      (trace_map_prop := traceMapEvalAtRootsIs1) (fintypeCard := fieldFintypeCard)
  let newVec := u ::·µ• newElts
  let firstElementOfVecIsSpecialElement : newVec.1.headI = u := rfl
  let btResult : BinaryTowerResult curBTField (k + 1) := {
    vec := newVec,
    instField := instFieldAdjoinRootOfPoly,
    firstElementOfVecIsSpecialElement := firstElementOfVecIsSpecialElement,
    instIrreduciblePoly := instIrreduciblePoly,
    sumZeroIffEq := sumZeroIffEq,
    specialElement := u,
    specialElementNeZero := inst_u_NeZero,
    instFintype := instFintype,
    fieldFintypeCard := fieldFintypeCard,
    traceMapEvalAtRootsIs1 := traceMapEvalAtRootsIs1
  }

  rw [‚Üêmul_comm] at eval_prevPoly_at_root

  let btInductiveStepResult : BinaryTowerInductiveStepResult (k:=k) (prevBTField:=prevBTField)
    (prevBTResult:=prevBTResult) (prevPoly:=definingPoly t1)
    (F:=curBTField) (instPrevBTFieldIsField:=prevBTResult.instField) := {
    binaryTowerResult := btResult,
    eq_adjoin := adjoinRootOfPoly
    u_is_root := u_is_root,
    eval_defining_poly_at_root := eval_prevPoly_at_root
  }

  exact ‚ü®curBTField, btInductiveStepResult‚ü©

def BinaryTowerAux (k : ‚Ñï) : (Œ£' (F : Type 0), BinaryTowerResult F k) :=
  match k with
  | 0 => -- Base Case : k = 0
    let curBTField := GF(2)
    let newList : List.Vector (GF(2)) 1 := List.Vector.cons (1 : GF(2)) List.Vector.nil
    let specialElement : GF(2) := newList.1.headI
    let firstElementOfVecIsSpecialElement : newList.1.headI = specialElement := rfl
    let specialElementIs1 : specialElement = 1 := by
      unfold specialElement
      rfl
    let specialElementNeZero : NeZero specialElement := {
      out := by
        rw [specialElementIs1]
        norm_num
    }
    let newPoly : curBTField[X] := definingPoly (F:=curBTField) (s:=specialElement)
    have nat_deg_poly_is_2 : newPoly.natDegree = 2 := natDegree_definingPoly specialElement
    have coeffOfX_0 : newPoly.coeff 0 = 1 := definingPoly_coeffOf0 specialElement
    have coeffOfX_1 : newPoly.coeff 1 = specialElement := definingPoly_coeffOf1 specialElement

    let instIrreduciblePoly : Irreducible newPoly := by
      by_contra h_not_irreducible
      -- ¬¨Irreducible p ‚Üî ‚àÉ c‚ÇÅ c‚ÇÇ, p.coeff 0 = c‚ÇÅ * c‚ÇÇ ‚àß p.coeff 1 = c‚ÇÅ + c‚ÇÇ
      obtain ‚ü®c‚ÇÅ, c‚ÇÇ, h_mul, h_add‚ü© :=
        (Monic.not_irreducible_iff_exists_add_mul_eq_coeff
          (definingPoly_is_monic specialElement) (nat_deg_poly_is_2)).mp h_not_irreducible
      rw [coeffOfX_0] at h_mul -- 1 = c‚ÇÅ * c‚ÇÇ
      rw [coeffOfX_1] at h_add -- specialElement = c‚ÇÅ + c‚ÇÇ
      -- since c‚ÇÅ, c‚ÇÇ ‚àà GF(2), c‚ÇÅ * c‚ÇÇ = 1 => c‚ÇÅ = c‚ÇÇ = 1
      have c1_c2_eq_one : c‚ÇÅ = 1 ‚àß c‚ÇÇ = 1 := by
        -- In GF(2), elements are only 0 or 1
        have c1_cases : c‚ÇÅ = 0 ‚à® c‚ÇÅ = 1 := by exact GF_2_value_eq_zero_or_one c‚ÇÅ
        have c2_cases : c‚ÇÇ = 0 ‚à® c‚ÇÇ = 1 := by exact GF_2_value_eq_zero_or_one c‚ÇÇ

        -- Case analysis on c‚ÇÅ and c‚ÇÇ
        rcases c1_cases with c1_zero | c1_one
        ¬∑ -- If c‚ÇÅ = 0
          rw [c1_zero] at h_mul
          -- Then 0 * c‚ÇÇ = 1, contradiction
          simp at h_mul
        ¬∑ -- If c‚ÇÅ = 1
          rcases c2_cases with c2_zero | c2_one
          ¬∑ -- If c‚ÇÇ = 0
            rw [c2_zero] at h_mul
            -- Then 1 * 0 = 1, contradiction
            simp at h_mul
          ¬∑ -- If c‚ÇÇ = 1
            -- Then we have our result
            exact ‚ü®c1_one, c2_one‚ü©

      -- Now we can show specialElement = 0
      have specialElement_eq_zero : specialElement = 0 := by
        rw [h_add]  -- Use c‚ÇÅ + c‚ÇÇ = specialElement
        rw [c1_c2_eq_one.1, c1_c2_eq_one.2]  -- Replace c‚ÇÅ and c‚ÇÇ with 1
        -- In GF(2), 1 + 1 = 0
        apply GF_2_one_add_one_eq_zero

      -- But we know specialElement = 1
      have specialElement_eq_one : specialElement = 1 := by
        unfold specialElement
        simp [newList]

      rw [specialElement_eq_zero] at specialElement_eq_one
      -- (0 : GF(2)) = (1 : GF(2))

      have one_ne_zero_in_gf2 : (1 : GF(2)) ‚â† (0 : GF(2)) := by
        exact NeZero.out
      contradiction

    let sumZeroIffEq : ‚àÄ (x y : GF(2)), x + y = 0 ‚Üî x = y := by
      intro x y
      constructor
      ¬∑ -- (‚Üí) If x + y = 0, then x = y
        intro h_sum_zero
        -- Case analysis on x
        rcases GF_2_value_eq_zero_or_one x with x_zero | x_one
        ¬∑ -- Case x = 0
          rcases GF_2_value_eq_zero_or_one y with y_zero | y_one
          ¬∑ -- Case y = 0
            rw [x_zero, y_zero]
          ¬∑ -- Case y = 1
            rw [x_zero, y_one] at h_sum_zero
            -- 0 + 1 = 0
            simp at h_sum_zero
        ¬∑ -- Case x = 1
          rcases GF_2_value_eq_zero_or_one y with y_zero | y_one
          ¬∑ -- Case y = 0
            rw [x_one, y_zero] at h_sum_zero
            -- 1 + 0 = 0
            simp at h_sum_zero
          ¬∑ -- Case y = 1
            rw [x_one, y_one]
      ¬∑ -- (‚Üê) If x = y, then x + y = 0
        intro h_eq
        rw [h_eq]
        -- In GF(2), x + x = 0 for any x
        rcases GF_2_value_eq_zero_or_one y with y_zero | y_one
        ¬∑ rw [y_zero]
          simp
        ¬∑ rw [y_one]
          exact GF_2_one_add_one_eq_zero
    let instFintype : Fintype (GF(2)) := GF_2_fintype
    let fieldFintypeCard : Fintype.card (GF(2)) = 2^(2^0) := by exact GF_2_card
    have traceMapEvalAtRootsIs1 : (‚àë i ‚àà Finset.range (2^0), specialElement^(2^i)) = 1
      ‚àß (‚àë i ‚àà Finset.range (2^0), (specialElement‚Åª¬π)^(2^i)) = 1 := by
      constructor
      ¬∑ -- Prove first part : (‚àë i ‚àà Finset.range (2^0), specialElement^(2^i)) = 1
        rw [Nat.pow_zero] -- 2^0 = 1
        rw [Finset.range_one] -- range 1 = {0}
        rw [specialElementIs1] -- specialElement = 1
        norm_num
      ¬∑ -- Prove second part : (‚àë i ‚àà Finset.range (2^0), (specialElement‚Åª¬π)^(2^i)) = 1
        rw [Nat.pow_zero] -- 2^0 = 1
        simp [Finset.range_one] -- range 1 = {0}
        exact specialElementIs1

    let result : BinaryTowerResult curBTField 0 :={
      vec := newList,
      instField := inferInstance,
      specialElement := specialElement,
      specialElementNeZero := specialElementNeZero,
      firstElementOfVecIsSpecialElement := firstElementOfVecIsSpecialElement,
      instIrreduciblePoly := instIrreduciblePoly,
      sumZeroIffEq := sumZeroIffEq,
      instFintype := instFintype,
      fieldFintypeCard := fieldFintypeCard,
      traceMapEvalAtRootsIs1 := ‚ü®traceMapEvalAtRootsIs1.1, traceMapEvalAtRootsIs1.2‚ü©
    }

    ‚ü® curBTField, result ‚ü©
  | k + 1 => by
    let prev := BinaryTowerAux (k:=k)
    let prevBTResult := prev.2
    let instPrevBTield := prevBTResult.instField
    let inductive_result := binary_tower_inductive_step (k:=k)
      (prevBTField:=prev.fst) (prevBTResult:=prev.snd)
    let res : (F : Type) √ó' BinaryTowerResult F (k + 1) :=
      ‚ü® inductive_result.fst, inductive_result.snd.binaryTowerResult ‚ü©
    exact res

@[simp]
def BTField (k : ‚Ñï) := (BinaryTowerAux k).1

lemma BTField_is_BTFieldAux (k : ‚Ñï) :
  BTField k = (BinaryTowerAux k).1 := by
  unfold BTField
  rfl

@[simp]
instance BTFieldIsField (k : ‚Ñï) : Field (BTField k) := (BinaryTowerAux k).2.instField

@[simp]
instance CommRing (k : ‚Ñï) : CommRing (BTField k) := Field.toCommRing

@[simp]
instance Nontrivial (k : ‚Ñï) : Nontrivial (BTField k) := inferInstance

@[simp]
instance Inhabited (k : ‚Ñï) : Inhabited (BTField k) where
  default := (0 : BTField k)

instance {k : ‚Ñï} : _root_.Inhabited (BinaryTowerAux k).fst := by
  change _root_.Inhabited (BTField k)
  exact Inhabited k

@[simp]
instance BTFieldNeZero1 (k : ‚Ñï) : NeZero (1 : BTField k) := by
  unfold BTField
  exact @neZero_one_of_nontrivial_comm_monoid_zero (BTField k) _ (Nontrivial k)

@[simp]
instance BTField_Fintype (k : ‚Ñï) : Fintype (BTField k) := (BinaryTowerAux k).2.instFintype

@[simp]
def BTFieldCard (k : ‚Ñï) : Fintype.card (BTField k) = 2^(2^k)
  := (BinaryTowerAux k).2.fieldFintypeCard

@[simp]
instance BTFieldIsDomain (k : ‚Ñï) : IsDomain (BTField k) := inferInstance

@[simp]
instance BTFieldNoZeroDiv (k : ‚Ñï) : NoZeroDivisors (BTField k) := by
  unfold BTField
  infer_instance

@[simp]
def sumZeroIffEq (k : ‚Ñï) : ‚àÄ (x y : BTField k),
  x + y = 0 ‚Üî x = y := (BinaryTowerAux k).2.sumZeroIffEq

@[simp]
instance BTFieldChar2 (k : ‚Ñï) : CharP (BTField k) 2 :=
  charP_eq_2_of_add_self_eq_zero (sumZeroIffEq:=sumZeroIffEq k)

@[simp]
theorem BTField_0_is_GF_2 : (BTField 0) = (GF(2)) := by
  unfold BTField
  rw [BinaryTowerAux]

@[simp]
def list (k : ‚Ñï) : List.Vector (BTField k) (k + 1) := (BinaryTowerAux k).2.vec

/-- Z k is the generator of BTField k -/
@[simp]
def Z (k : ‚Ñï) : BTField k := (BinaryTowerAux k).snd.specialElement -- (list k).1.headI

instance {k : ‚Ñï} : NeZero (Z k) := (BinaryTowerAux k).snd.specialElementNeZero

@[simp]
def poly (k : ‚Ñï) : Polynomial (BTField k) := definingPoly (s:=(Z k))

lemma poly_natDegree_eq_2 (k : ‚Ñï) : (poly (k:=k)).natDegree = 2 :=
  natDegree_eq_of_degree_eq_some (degree_definingPoly (Z k))

lemma BTField.cast_BTField_eq (k m : ‚Ñï) (h_eq : k = m) :
  BTField k = BTField m := by
  subst h_eq
  rfl

lemma BTField.cast_mul (m n : ‚Ñï) {x y : BTField m} (h_eq : m = n) :
  (cast (by exact BTField.cast_BTField_eq m n h_eq) (x * y)) =
  (cast (by exact BTField.cast_BTField_eq m n h_eq) x) *
  (cast (by exact BTField.cast_BTField_eq m n h_eq) y) := by
  subst h_eq
  rfl

/-- adjoined root of poly k, generator of successor field BTField (k+1) -/
@[simp]
def ùïè (k : ‚Ñï) : BTField (k+1) := Z (k+1)

@[coe]
theorem BTField_succ_eq_adjoinRoot (k : ‚Ñï) : AdjoinRoot (poly k) = BTField (k+1) := rfl

instance coe_field_adjoinRoot (k : ‚Ñï) : Coe (AdjoinRoot (poly k)) (BTField (k+1)) where
  coe := Eq.mp (BTField_succ_eq_adjoinRoot k)

@[simp]
theorem Z_succ_eq_adjointRoot_root (k : ‚Ñï) : Z (k+1) = AdjoinRoot.root (poly k) := rfl

lemma list_0 : list 0 = List.Vector.cons (1 : GF(2)) List.Vector.nil := by
  unfold list
  rfl

@[simp]
lemma list_eq (k : ‚Ñï) :
  list (k+1) = (Z (k+1)) ::·µ• (list k).map (AdjoinRoot.of (poly k)) := by
  unfold list
  rfl

@[simp]
theorem eval_poly_at_root (k : ‚Ñï) : (Z (k+1))^2 + (Z (k+1)) * Z k + 1 = 0 := by
  let btResult := BinaryTowerAux k
  let _instPrevBTield := btResult.2.instField
  let step := binary_tower_inductive_step k btResult.fst btResult.snd
  let eq := step.snd.eval_defining_poly_at_root
  exact eq

@[simp]
theorem poly_form (k : ‚Ñï) : poly k = X^2 + (C (Z k) * X + 1) := by rw [‚Üêadd_assoc]; rfl

@[simp]
theorem eval_mapped_poly_at_root (k : ‚Ñï) :
    eval‚ÇÇ (AdjoinRoot.of (poly k)) (Z (k+1)) (poly k) = 0 := by
  have h_BTField_succ_eq_adjoinRoot : BTField (k+1) = AdjoinRoot (poly k) :=
    BTField_succ_eq_adjoinRoot k
  have h_poly_form : poly k = X^2 + (C (Z k) * X + 1) := poly_form k
  -- ‚ä¢ eval‚ÇÇ (of (poly k)) (Z (k + 1)) (poly k) = 0
  -- NOTE : we explicitly use the output of coercion as BTField (k+1)
  -- instead of AdjoinRoot (poly k) for consistency
  set of_prev : BTField k ‚Üí+* BTField (k+1) := AdjoinRoot.of (poly k)
  calc
    eval‚ÇÇ of_prev (Z (k+1)) (poly k) = eval‚ÇÇ of_prev (Z (k+1)) (X^2 + (C (Z k) * X + 1)) := by
      rw [‚Üêh_poly_form]
    _ = eval‚ÇÇ of_prev (Z (k+1)) (X^2) + eval‚ÇÇ of_prev (Z (k+1)) (C (Z k) * X)
      + eval‚ÇÇ of_prev (Z (k+1)) 1 := by
      rw [eval‚ÇÇ_add, add_assoc, eval‚ÇÇ_add]
    _ = (Z (k+1))^2 + (of_prev (Z k)) * (Z (k+1)) + 1 := by
      rw [eval‚ÇÇ_pow, eval‚ÇÇ_mul, eval‚ÇÇ_C, eval‚ÇÇ_X, eval‚ÇÇ_one]
    _ = (Z (k+1))^2 + (Z (k+1)) * (of_prev (Z k)) + 1 := by
      rw [mul_comm]
    _ = (Z (k+1))^2 + (Z (k+1)) * (Z k) + 1 := by rfl -- x * (algegraMap scalar) = x * scalar
    _ = 0 := by
      rw [‚Üêeval_poly_at_root k]

@[simp]
lemma list_length (k : ‚Ñï) : (list k).length = k + 1 := by
  unfold list
  rfl

@[simp]
theorem list_nonempty (k : ‚Ñï) : (list k).1 ‚â† [] := by
  by_contra h_empty
  have h_len := list_length k -- h_len : (list k).length = k + 1
  have h_len_zero := List.length_eq_zero_iff.mpr h_empty -- h_len_zero : (‚Üë(list k)).length = 0
  have h_len_eq : (list k).length = List.length ((list k).1) := by
    simp only [BTField, list, list_length, List.Vector.length_val]
  rw [h_len_eq, h_len_zero] at h_len
  have : k + 1 ‚â† 0 := Nat.succ_ne_zero k
  contradiction

instance polyIrreducible (n : ‚Ñï) : Irreducible (poly n) := (BinaryTowerAux n).2.instIrreduciblePoly

instance polyIrreducibleFact (n : ‚Ñï) : Fact (Irreducible (poly n)) := ‚ü®polyIrreducible n‚ü©

instance polyMonic (n : ‚Ñï) : Monic (poly n) := definingPoly_is_monic (Z n)

lemma poly_ne_zero (n : ‚Ñï) : poly n ‚â† 0 := (polyMonic n).ne_zero

end BTFieldDefs

section BinaryAlgebraTower
/--
The canonical ring homomorphism embedding `BTField k` into `BTField (k+1)`.
This is the `AdjoinRoot.of` map.
-/
def canonicalEmbedding (k : ‚Ñï) : BTField k ‚Üí+* BTField (k+1) :=
  AdjoinRoot.of (poly k)

@[simp]
lemma BTField_add_eq (k n m) : BTField (k + n + m) = BTField (k + (n + m)) := by
  rw [add_assoc]

@[simp]
theorem BTField.RingHom_eq_of_dest_eq (k m n : ‚Ñï) (h_eq : m = n) :
  (BTField k ‚Üí+* BTField m) = (BTField k ‚Üí+* BTField n) := by
  subst h_eq
  rfl

@[simp]
theorem BTField.RingHom_eq_of_dest_AdjoinRoot_eq (k m : ‚Ñï) :
  (BTField k ‚Üí+* BTField (m+1)) = (BTField k ‚Üí+* (AdjoinRoot (poly m))) := by
  rw! (castMode:=.all) [BTField_succ_eq_adjoinRoot m]

@[simp]
theorem BTField.RingHom_cast_dest_apply (k m n : ‚Ñï) (h_eq : m = n)
  (f : BTField k ‚Üí+* BTField m) (x : BTField k) :
    (cast (BTField.RingHom_eq_of_dest_eq (k:=k) (m:=m) (n:=n) h_eq) f) x
    = cast (by apply cast_BTField_eq (h_eq:=h_eq)) (f x) := by
  subst h_eq
  rfl

@[simp]
theorem BTField.RingHom_cast_dest_AdjoinRoot_apply (k m : ‚Ñï)
  (f : BTField k ‚Üí+* AdjoinRoot (poly m)) (x : BTField k) :
  (cast (BTField.RingHom_eq_of_dest_AdjoinRoot_eq (k:=k) (m:=m)).symm f) x
  = cast (BTField_succ_eq_adjoinRoot m) (f x) := by
  rfl

/--
Auxiliary definition for `towerAlgebraMap` using structural recursion.
This is easier to reason about in proofs than the `Nat.rec` version.
TODO : migrate to Fin.dfoldl
-/
def towerAlgebraMap (l r : ‚Ñï) (h_le : l ‚â§ r) : BTField l ‚Üí+* BTField r := by
  if h_lt : l = r then
    subst h_lt
    exact RingHom.id (BTField l)
  else
    let map_to_r_sub_1 : BTField l ‚Üí+* BTField (r - 1) := towerAlgebraMap (h_le:=by omega)
    let next_embedding : BTField (r - 1) ‚Üí+* BTField r := by
      have ringHomEq := BTField.RingHom_eq_of_dest_eq (k:=r-1) (m:=r) (n:=r - 1 + 1) (by omega)
      exact Eq.mp ringHomEq.symm (canonicalEmbedding (r - 1))
    exact next_embedding.comp map_to_r_sub_1

lemma towerAlgebraMap_id (k : ‚Ñï) : towerAlgebraMap (h_le:=by omega) = RingHom.id (BTField k) := by
  unfold towerAlgebraMap
  exact (Ne.dite_eq_left_iff fun h a ‚Ü¶ h rfl).mpr rfl

lemma towerAlgebraMap_succ_1 (k : ‚Ñï) :
  towerAlgebraMap (l:=k) (r:=k+1) (h_le:=by omega) = canonicalEmbedding k := by
  unfold towerAlgebraMap
  simp only [Nat.left_eq_add, one_ne_zero, ‚ÜìreduceDIte,
    Nat.add_one_sub_one, eq_mp_eq_cast, cast_eq]
  rw [towerAlgebraMap_id]
  rw [RingHom.comp_id]

/-! Right associativity of the Tower Map -/
lemma towerAlgebraMap_succ (l r : ‚Ñï) (h_le : l ‚â§ r) :
  towerAlgebraMap (l:=l) (r:=r+1) (h_le:=by omega) =
  (towerAlgebraMap (l:=r) (r:=r+1) (h_le:=by omega)).comp
  (towerAlgebraMap (l:=l) (r:=r) (h_le:=by omega)) := by
  ext x
  conv_lhs => rw [towerAlgebraMap]
  have h_l_ne_eq_r_add_1 : l ‚â† r + 1 := by omega
  simp only [h_l_ne_eq_r_add_1, ‚ÜìreduceDIte, Nat.add_one_sub_one,
    eq_mp_eq_cast, cast_eq, RingHom.coe_comp, Function.comp_apply]
  rw [towerAlgebraMap_succ_1]

/-! Left associativity of the Tower Map -/
theorem towerAlgebraMap_succ_last (r : ‚Ñï) : ‚àÄ l : ‚Ñï, (h_le : l ‚â§ r) ‚Üí
  towerAlgebraMap (l:=l) (r:=r+1) (h_le:=by
    exact Nat.le_trans (n:=l) (m:=r) (k:=r+1) (h_le) (by omega)) =
  (towerAlgebraMap (l:=l+1) (r:=r+1) (by omega)).comp (towerAlgebraMap
    (l:=l) (r:=l+1) (by omega)) := by
  induction r using Nat.strong_induction_on with
  | h r ih_r => -- prove for width = r + 1
    intro l h_le
    if h_l_eq_r : l = r then
      subst h_l_eq_r
      rw [towerAlgebraMap_id, RingHom.id_comp]
    else
      -- A = |l| --- (1) --- |l+1| --- (2) --- |r| --- (3) --- |r+1|
      -- ‚ä¢ towerMap l (r + 1) = (towerMap (l + 1) r).comp (towerMap l l+1) => ‚ä¢ A = (23) ‚àò (1)
      -- Proof : A = 3 ‚àò (12) (succ decomposition) = 3 ‚àò (2 ‚àò 1) (ind of width = r)
      rw [towerAlgebraMap_succ (l:=l) (r:=r) (by omega)]
      have h_l_r := ih_r (m:=r-1) (l:=l) (h_le:=by omega) (by omega)
      have h_r_sub_1_add_1 : r - 1 + 1 = r := by omega
      rw! [h_r_sub_1_add_1] at h_l_r
      rw [h_l_r, ‚ÜêRingHom.comp_assoc, ‚ÜêtowerAlgebraMap_succ]

/--
Cast of composition of BTField ring homomorphism is composition of casted BTField ring homomorphism.
Note that this assumes the SAME underlying instances (e.g. NonAssocSemiring)
for both the input and output ring homs.
-/
@[simp]
theorem BTField.RingHom_comp_cast {Œ± Œ≤ Œ≥ Œ¥ : ‚Ñï} (f : BTField Œ± ‚Üí+* BTField Œ≤)
  (g : BTField Œ≤ ‚Üí+* BTField Œ≥) (h : Œ≥ = Œ¥) :
    ((cast (BTField.RingHom_eq_of_dest_eq (k:=Œ≤) (m:=Œ≥) (n:=Œ¥) h) g).comp f)
    = cast (BTField.RingHom_eq_of_dest_eq (k:=Œ±) (m:=Œ≥) (n:=Œ¥) h) (g.comp f) := by
  have h1 := BTField.RingHom_eq_of_dest_eq (k:=Œ≤) (m:=Œ≥) (n:=Œ¥) h
  have h2 := BTField.RingHom_eq_of_dest_eq (k:=Œ±) (m:=Œ≥) (n:=Œ¥) h
  have h_heq : HEq ((cast (h1) g).comp f) (cast (h2) (g.comp f)) := by
    subst h -- this simplifies h1 h2 in cast which makes them trivial equality
      -- => hence it becomes easier to simplify
    simp only [BTField, BTFieldIsField, cast_eq, heq_eq_eq]
  apply eq_of_heq h_heq

theorem towerAlgebraMap_assoc : ‚àÄ r mid l : ‚Ñï, (h_l_le_mid : l ‚â§ mid) ‚Üí (h_mid_le_r : mid ‚â§ r) ‚Üí
    towerAlgebraMap (l:=l) (r:=r) (h_le:=by exact Nat.le_trans h_l_le_mid h_mid_le_r) =
    (towerAlgebraMap (l:=mid) (r:=r) (h_le:=h_mid_le_r)).comp
    (towerAlgebraMap (l:=l) (r:=mid) (h_le:=h_l_le_mid)) := by
  -- We induct on `r`, keeping `l` and `mid` as variables in the induction hypothesis.
  intro r
  induction r using Nat.strong_induction_on with
  | h r ih_r => -- right width = r, left width = l
    intro mid l h_l_le_mid h_mid_le_r
    -- A = |l| --- (1) --- |mid| --- (2) --- |r-1| --- (3) --- |r|
    -- Proof : A = 3 ‚àò (12) (succ decomposition) = 3 ‚àò (2 ‚àò 1) (induction hypothesis)
    -- = (3 ‚àò 2) ‚àò 1 = (23) ‚àò 1 (succ decomp) (Q.E.D)
    if h_mid_eq_r : mid = r then
      subst h_mid_eq_r
      simp only [towerAlgebraMap_id, RingHom.id_comp]
    else
      have h_mid_lt_r : mid < r := by omega
      set r_sub_1 := r - 1 with hr_sub_1
      have h_r_sub_1_add_1 : r_sub_1 + 1 = r := by omega
      -- A = 3 ‚àò (12)
      rw! [h_r_sub_1_add_1.symm]
      rw [towerAlgebraMap_succ (l:=l) (r:=r_sub_1) (by omega)]
      -- A = 3 ‚àò (2 ‚àò 1)
      have right_split := ih_r (m:=r_sub_1) (l:=l) (mid:=mid) (by omega) (by omega) (by omega)
      rw [right_split, ‚ÜêRingHom.comp_assoc]
      -- A = (23) ‚àò 1
      rw [‚ÜêtowerAlgebraMap_succ]
/--
**Formalization of Cross-Level Algebra** : For any `k ‚â§ œÑ`, `BTField œÑ` is an
algebra over `BTField k`.
-/
instance : AlgebraTower (BTField) where
  algebraMap := towerAlgebraMap
  commutes' := by
    intro i j h r x
    exact CommMonoid.mul_comm ((towerAlgebraMap i j h) r) x
  coherence' := by exact fun i j k h1 h2 ‚Ü¶ towerAlgebraMap_assoc k j i h1 h2

def binaryAlgebraTower {l r : ‚Ñï} (h_le : l ‚â§ r) : Algebra (BTField l) (BTField r) := by
  exact AlgebraTower.toAlgebra h_le

lemma binaryTowerAlgebra_def (l r : ‚Ñï) (h_le : l ‚â§ r) :
    @binaryAlgebraTower (l:=l) (r:=r) (h_le:=h_le)
    = (towerAlgebraMap l r h_le).toAlgebra := by rfl

lemma algebraMap_binaryTowerAlgebra_def (l r : ‚Ñï) (h_le : l ‚â§ r) :
  (@binaryAlgebraTower (l:=l) (r:=r) (h_le:=h_le)).algebraMap = towerAlgebraMap l r h_le := by rfl

lemma BTField.coe_one_succ (l : ‚Ñï) :
  (@binaryAlgebraTower (l:=l) (r:=l+1) (h_le:=by omega)).algebraMap (1 : BTField l) =
    (1 : BTField (l+1)) := by
  exact RingHom.map_one (binaryAlgebraTower (l:=l) (r:=l+1) (h_le:=by omega)).algebraMap

@[simp]
theorem binaryTowerAlgebra_id {l r : ‚Ñï} (h_eq : l = r) :
    @binaryAlgebraTower l r (h_le:=by omega) =
    (h_eq ‚ñ∏ (Algebra.id (BTField l)) : Algebra (BTField l) (BTField r)) := by
  subst h_eq
  simp only [binaryTowerAlgebra_def, towerAlgebraMap_id]
  rfl

theorem binaryTowerAlgebra_apply_assoc (l mid r : ‚Ñï) (h_l_le_mid : l ‚â§ mid) (h_mid_le_r : mid ‚â§ r) :
    ‚àÄ x : BTField l,
    (@binaryAlgebraTower (l:=l) (r:=r) (h_le:=by
      exact Nat.le_trans h_l_le_mid h_mid_le_r)).algebraMap x =
    (@binaryAlgebraTower (l:=mid) (r:=r) (h_le:=h_mid_le_r)).algebraMap
      ((@binaryAlgebraTower (l:=l) (r:=mid) (h_le:=h_l_le_mid)).algebraMap x)
    := by
  intro x
  simp_rw [algebraMap_binaryTowerAlgebra_def]
  rw [‚ÜêRingHom.comp_apply]
  rw [towerAlgebraMap_assoc (l:=l) (mid:=mid) (r:=r)
    (h_l_le_mid:=h_l_le_mid) (h_mid_le_r:=h_mid_le_r)]

/-- This also provides the corresponding Module instance. -/
def binaryTowerModule {l r : ‚Ñï} (h_le : l ‚â§ r) : Module (BTField l) (BTField r) :=
  (binaryAlgebraTower (h_le:=h_le)).toModule

instance (priority := 1000) algebra_adjacent_tower (l : ‚Ñï) :
  Algebra (BTField l) (BTField (l+1)) := by
  exact binaryAlgebraTower (h_le:=by omega)

lemma algebraMap_adjacent_tower_def (l : ‚Ñï) :
  (algebraMap (BTField l) (BTField (l + 1))) = canonicalEmbedding l := by
  unfold algebra_adjacent_tower
  rw [binaryTowerAlgebra_def]
  exact towerAlgebraMap_succ_1 l

lemma algebraMap_adjacent_tower_succ_eq_Adjoin_of (k : ‚Ñï) :
  (algebraMap (BTField k) (BTField (k + 1))) = of (poly k) := by
  rw [algebraMap_adjacent_tower_def]
  rfl

lemma algebra_adjacent_tower_def (l : ‚Ñï) :
  (algebra_adjacent_tower l) = (canonicalEmbedding l).toAlgebra := by
  unfold algebra_adjacent_tower
  rw [binaryTowerAlgebra_def]
  rw [towerAlgebraMap_succ_1]

lemma algebra_adjacent_tower_eq_AdjoinRoot_algebra (k : ‚Ñï) :
  (algebra_adjacent_tower k) = (AdjoinRoot.instAlgebra (poly k)) := by
  rw [algebra_adjacent_tower_def]
  unfold canonicalEmbedding
  rw [‚ÜêAdjoinRoot.algebraMap_eq]
  rw [algebraMap, Algebra.algebraMap]
  exact
    Algebra.algebra_ext (AdjoinRoot.instAlgebra (poly k)).2.toAlgebra
      (AdjoinRoot.instAlgebra (poly k)) (congrFun rfl)

def BTField_succ_alg_equiv_adjoinRoot (k : ‚Ñï) :
  AdjoinRoot (poly k) ‚âÉ‚Çê[BTField k] BTField (k + 1) := by
  have h_eq : AdjoinRoot (poly k) = BTField (k + 1) := BTField_succ_eq_adjoinRoot k
  exact { -- We can construct RingEquiv in a similar way
    toFun     := Equiv.cast h_eq,
    invFun    := Equiv.cast h_eq.symm,
    left_inv  := by { intro x; cases h_eq; rfl },
    right_inv := by { intro x; cases h_eq; rfl },
    map_mul'  := by { intros x y; cases h_eq; rfl },
    map_add'  := by { intros x y; cases h_eq; rfl },
    commutes' := by {
      intros r
      rw [algebraMap_adjacent_tower_def]
      rfl -- canonicalEmbedding is compatible with AdjoinRoot
    }
  }

end BinaryAlgebraTower

noncomputable section MultilinearBasis

@[simp]
theorem BTField.Basis_cast_index_eq (i j k n : ‚Ñï) (h_le : k ‚â§ n) (h_eq : i = j) :
    letI instAlgebra : Algebra (BTField k) (BTField n)
      := binaryAlgebraTower (l:=k) (r:=n) (h_le:=h_le)
    letI : Module (BTField k) (BTField n) := instAlgebra.toModule
    (Basis (Fin (i)) (BTField k) (BTField n)) = (Basis (Fin (j)) (BTField k) (BTField n)) := by
  subst h_eq
  rfl

theorem BTField.Basis_cast_dest_eq {Œπ : Type*} (k n m : ‚Ñï) (h_k_le_n : k ‚â§ n)
  (h_k_le_m : k ‚â§ m) (h_eq : m = n) :
  letI instLeftAlgebra := binaryAlgebraTower (l:=k) (r:=m) (h_le:=h_k_le_m)
  letI instRightAlgebra := binaryAlgebraTower (l:=k) (r:=n) (h_le:=h_k_le_n)
  @Basis Œπ (BTField k) (BTField m) _ _ instLeftAlgebra.toModule =
  @Basis Œπ (BTField k) (BTField n) _ _ instRightAlgebra.toModule := by
  subst h_eq
  rfl

theorem BTField.PowerBasis_cast_dest_eq (k n m : ‚Ñï) (h_k_le_n : k ‚â§ n)
  (h_k_le_m : k ‚â§ m) (h_eq : m = n) :
  letI instLeftAlgebra := binaryAlgebraTower (l:=k) (r:=m) (h_le:=h_k_le_m)
  letI instRightAlgebra := binaryAlgebraTower (l:=k) (r:=n) (h_le:=h_k_le_n)
  @PowerBasis (BTField k) (BTField m) _ _ instLeftAlgebra =
  @PowerBasis (BTField k) (BTField n) _ _ instRightAlgebra := by
  subst h_eq
  rfl
/-!
The following two theorems are used to cast the basis of `BTField Œ±` to `BTField Œ≤`
via changing in index type : `Fin (i)` to `Fin (j)` when `Œ± ‚â§ Œ≤`.
-/
@[simp]
theorem BTField.Basis_cast_index_apply {Œ± Œ≤ i j : ‚Ñï} {k : Fin j} (h_le : Œ± ‚â§ Œ≤) (h_eq : i = j)
  {b : @Basis (Fin (i)) (BTField Œ±) (BTField Œ≤) _ _
    (@binaryAlgebraTower (l := Œ±) (r := Œ≤) (h_le := h_le)).toModule} :
  let castBasis : @Basis (Fin j) (BTField Œ±) (BTField Œ≤) _ _
    (@binaryAlgebraTower (l:=Œ±) (r:=Œ≤) (h_le:=h_le)).toModule :=
    cast (by exact BTField.Basis_cast_index_eq i j Œ± Œ≤ h_le h_eq) b
  (castBasis k) = b (Fin.cast (h_eq.symm) k) := by
  subst h_eq
  rfl

@[simp]
theorem BTField.Basis_cast_dest_apply {Œπ : Type*} (Œ± Œ≤ Œ≥ : ‚Ñï) (h_le1 : Œ± ‚â§ Œ≤) (h_le2 : Œ± ‚â§ Œ≥)
    (h_eq : Œ≤ = Œ≥) {k : Œπ} (b : @Basis Œπ (BTField Œ±) (BTField Œ≤) _ _
    (@binaryAlgebraTower (l := Œ±) (r := Œ≤) (h_le := h_le1)).toModule) :
    let castBasis : @Basis Œπ (BTField Œ±) (BTField Œ≥) _ _
      (@binaryAlgebraTower (l := Œ±) (r := Œ≥) (h_le := h_le2)).toModule :=
      cast (by
        exact Basis_cast_dest_eq Œ± Œ≥ Œ≤ h_le2 h_le1 h_eq
      ) b
    (castBasis k) = cast (by exact BTField.cast_BTField_eq Œ≤ Œ≥ h_eq) (b k) := by
  subst h_eq
  rfl

/-!
The power basis for `BTField (k+1)` over `BTField k` is {1, Z (k+1)}
-/
def powerBasisSucc (k : ‚Ñï) :
    PowerBasis (BTField k) (BTField (k+1)) := by
  let pb : PowerBasis (BTField k) (AdjoinRoot (poly k)) :=
    AdjoinRoot.powerBasis (hf:=by exact poly_ne_zero k)
  -- ‚ä¢ algebra_adjacent_tower k = AdjoinRoot.instAlgebra (poly k) => TODO : make a lemma for this
  -- NOTE : pb.gen is definitionally equal to AdjoinRoot.root (poly k)
  have h_eq : AdjoinRoot (poly k) = BTField (k+1) := BTField_succ_eq_adjoinRoot k
  -- ‚ä¢ PowerBasis (BTField k) (BTField (k + 1))
  apply pb.map (e:=BTField_succ_alg_equiv_adjoinRoot k)

lemma powerBasisSucc_gen (k : ‚Ñï) :
  (powerBasisSucc k).gen = (Z (k+1)) := by
  -- Z (k+1) is generator of BTField (k+1) over (BTField k)
  -- Correctness : Both sides are definitionally equal to AdjoinRoot.root (poly k)
  rfl

lemma powerBasisSucc_dim (k : ‚Ñï) :
  powerBasisSucc (k:=k).dim = 2 := by
  simp only [BTField, CommRing, BTFieldIsField, powerBasisSucc, poly, PowerBasis.map_dim,
    powerBasis_dim]
  exact natDegree_definingPoly (Z k)

def join_via_add_smul {k : ‚Ñï} (h_pos : k > 0) (hi_btf lo_btf : BTField (k - 1)) :
    BTField k := by
  letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  exact hi_btf ‚Ä¢ Z k + (algebraMap (BTField (k - 1)) (BTField k) lo_btf)

scoped[BinaryTower] notation "‚ãò" hi ", " lo "‚ãô" => join_via_add_smul (h_pos:=by omega) hi lo

lemma join_via_add_smul_zero {k : ‚Ñï} (h_pos : k > 0) :
  ‚ãò 0, 0 ‚ãô = 0 := by
  unfold join_via_add_smul
  simp only [map_zero, add_zero]
  letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  rw [Algebra.smul_def', map_zero, zero_mul]

lemma join_via_add_smul_one_zero_eq_Z {k : ‚Ñï} (h_pos : k > 0) :
  ‚ãò 1, 0 ‚ãô = Z k := by
  unfold join_via_add_smul
  letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  rw [Algebra.smul_def', map_one, map_zero, one_mul, add_zero]

lemma join_via_add_smul_one {k : ‚Ñï} (h_pos : k > 0) :
  ‚ãò 0, 1 ‚ãô = 1 := by
  unfold join_via_add_smul
  letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  rw [Algebra.smul_def', map_zero, map_one, zero_mul, zero_add]

theorem sum_join_via_add_smul (k : ‚Ñï) (h_pos : k > 0) (a‚ÇÅ a‚ÇÄ b‚ÇÅ b‚ÇÄ : BTField (k - 1)) :
  ‚ãò a‚ÇÅ, a‚ÇÄ ‚ãô + ‚ãò b‚ÇÅ, b‚ÇÄ ‚ãô = ‚ãò a‚ÇÅ + b‚ÇÅ, a‚ÇÄ + b‚ÇÄ ‚ãô := by
  letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  unfold join_via_add_smul
  simp only [map_add]
  rw [add_smul a‚ÇÅ b‚ÇÅ (Z k)]
  abel_nf

/--
(a‚ÇÅ ‚Ä¢ Z k + a‚ÇÄ) * (b‚ÇÅ ‚Ä¢ Z k + b‚ÇÄ)
= a‚ÇÅ * b‚ÇÅ ‚Ä¢ (Z k)^2 + (a‚ÇÅ * b‚ÇÄ + a‚ÇÄ * b‚ÇÅ) ‚Ä¢ Z k + a‚ÇÄ * b‚ÇÄ
= a‚ÇÅ * b‚ÇÅ ‚Ä¢ (Z (k-1) * Z k + 1) + (a‚ÇÅ * b‚ÇÄ + a‚ÇÄ * b‚ÇÅ) ‚Ä¢ Z k + a‚ÇÄ * b‚ÇÄ
= [a‚ÇÅ * b‚ÇÅ * Z (k - 1) + a‚ÇÅ * b‚ÇÄ + a‚ÇÄ * b‚ÇÅ] * (Z k) + (a‚ÇÄ * b‚ÇÄ + a‚ÇÅ * b‚ÇÅ)
-/
theorem mul_join_via_add_smul (k : ‚Ñï) (h_pos : k > 0) (a‚ÇÅ a‚ÇÄ b‚ÇÅ b‚ÇÄ : BTField (k - 1)) :
  ‚ãò a‚ÇÅ, a‚ÇÄ ‚ãô * ‚ãò b‚ÇÅ, b‚ÇÄ ‚ãô = ‚ãò a‚ÇÅ * b‚ÇÅ * Z (k - 1) + a‚ÇÅ * b‚ÇÄ + a‚ÇÄ * b‚ÇÅ, a‚ÇÄ * b‚ÇÄ + a‚ÇÅ * b‚ÇÅ ‚ãô := by
  letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  conv_lhs =>
    unfold join_via_add_smul
    rw [mul_add, add_mul, add_mul, ‚Üêmap_mul]
    rw [mul_comm (a‚ÇÅ ‚Ä¢ Z k) ((algebraMap (BTField (k - 1)) (BTField k)) b‚ÇÄ)]

  have h_a‚ÇÅ_b‚ÇÄ_Z_k : (algebraMap (BTField (k - 1)) (BTField k)) b‚ÇÄ * a‚ÇÅ ‚Ä¢ Z k
    = (a‚ÇÅ * b‚ÇÄ) ‚Ä¢ Z k := by
    rw [Algebra.smul_def', ‚ÜêalgebraMap, ‚Üêmul_assoc, ‚Üêmap_mul, ‚ÜêAlgebra.smul_def, mul_comm]
  have h_a‚ÇÄ_b‚ÇÅ_Z_k :  (algebraMap (BTField (k - 1)) (BTField k)) a‚ÇÄ * b‚ÇÅ ‚Ä¢ Z k
    = (a‚ÇÄ * b‚ÇÅ) ‚Ä¢ Z k := by
    rw [Algebra.smul_def', ‚ÜêalgebraMap, ‚Üêmul_assoc, ‚Üêmap_mul, ‚ÜêAlgebra.smul_def, mul_comm]
  have h_Z_k_pow_2 : (Z k) ^ 2 = Z (k - 1) ‚Ä¢ Z k + 1 := by
    rw [sumZeroIffEq (x:=(Z k)^2) (y:=Z (k - 1) ‚Ä¢ Z k + 1).mp]
    rw [‚Üêadd_assoc]
    rw [Algebra.smul_def', mul_comm]
    have h := eval_poly_at_root (k - 1)
    rw! (castMode:=.all) [Nat.sub_one_add_one (by omega)] at h
    simp only [eq_mp_eq_cast] at h
    convert h
    rw [Algebra.algebraMap]
    conv_lhs =>
      simp only [instAlgebra];
      change (towerAlgebraMap (l:=k-1) (r:=k) (h_le:=by omega)) (Z (k - 1))
    have h_towerMap_succ := towerAlgebraMap_succ_1 (k:=k-1)
    rw! (castMode:=.all) [Nat.sub_one_add_one (by omega)] at h_towerMap_succ
    rw [h_towerMap_succ]
    rw [eqRec_eq_cast, canonicalEmbedding]
    have h := BTField.RingHom_cast_dest_AdjoinRoot_apply
      (f:=AdjoinRoot.of (poly (k-1))) (x:=Z (k-1))
    rw! (castMode:=.all) [Nat.sub_one_add_one (by omega)] at h
    exact h
  have h_a‚ÇÅ_Z_k_b‚ÇÅ_Z_k : a‚ÇÅ ‚Ä¢ Z k * b‚ÇÅ ‚Ä¢ Z k
    = (a‚ÇÅ * b‚ÇÅ * Z (k - 1)) ‚Ä¢ Z k + (algebraMap (BTField (k - 1)) (BTField k)) (a‚ÇÅ * b‚ÇÅ) := by
    conv_lhs =>
      rw [Algebra.smul_def, Algebra.smul_def]
      rw [mul_comm ((algebraMap (BTField (k - 1)) (BTField k)) b‚ÇÅ) (Z k)]
      rw [‚Üêmul_assoc, mul_assoc ((algebraMap (BTField (k - 1)) (BTField k)) a‚ÇÅ) (Z k) (Z k)]
      rw [‚Üêpow_two, h_Z_k_pow_2, Algebra.smul_def, mul_add, add_mul, mul_one]
      rw [‚Üêmap_mul]
      rw [mul_comm, ‚Üêmul_assoc, ‚Üêmap_mul, ‚Üêmul_assoc, ‚Üêmap_mul, ‚ÜêAlgebra.smul_def, mul_comm b‚ÇÅ a‚ÇÅ]
  conv_lhs =>
    rw [h_a‚ÇÅ_b‚ÇÄ_Z_k, h_a‚ÇÄ_b‚ÇÅ_Z_k, h_a‚ÇÅ_Z_k_b‚ÇÅ_Z_k]
    rw [add_comm, add_comm ((a‚ÇÅ * b‚ÇÄ) ‚Ä¢ Z k), add_assoc, add_comm]
    rw [‚Üêadd_assoc, ‚Üêadd_assoc, ‚Üêadd_smul (x:=Z k)]
    rw [add_assoc (c:=(a‚ÇÄ * b‚ÇÅ) ‚Ä¢ Z k), add_comm (b:=(a‚ÇÄ * b‚ÇÅ) ‚Ä¢ Z k), ‚Üêadd_assoc, ‚Üêadd_smul]
    rw [add_assoc, ‚Üêmap_add]
    rw [add_comm (a‚ÇÅ * b‚ÇÄ), add_comm (a‚ÇÅ * b‚ÇÅ)]
  rfl

theorem unique_linear_decomposition_succ (k : ‚Ñï) :
  ‚àÄ (x : BTField (k+1)), ‚àÉ! (p : BTField k √ó BTField k),
    x = ‚ãò p.1, p.2 ‚ãô := by
  intro x
  -- First, we have `AdjoinRoot.powerBasis'` of dim 2 (`powerBasis'_dim`)
  -- Second, we represent `x` as a linear combination of the
  -- basis elements : `x = a0 + a1 * Z (k+1)`, this combination is unique
  -- Last, we prove the equality : `‚Ä¢` => `*`, `lo_btf` and `hi_btf` => `algebraMap`
  unfold join_via_add_smul
  simp only [Nat.add_one_sub_one]
  simp only [Algebra.smul_def]
  have unique_linear_combination : ‚àÄ (c1 : AdjoinRoot (poly k)),
    ‚àÉ! (p : BTField k √ó BTField k), c1 = (of (poly k)) p.1 * root (poly k) + (of (poly k)) p.2
      := by
    apply unique_linear_form_of_elements_in_adjoined_commring
    ¬∑ apply BinaryTower.poly_natDegree_eq_2
    ¬∑ apply BinaryTower.polyMonic
  let px := unique_linear_combination (c1:=x)
  simp_rw [algebraMap_adjacent_tower_succ_eq_Adjoin_of k]
  convert px

def split (k : ‚Ñï) (h_k : k > 0) (x : BTField k) : BTField (k-1) √ó BTField (k-1) := by
  have h_eq : k - 1 + 1 = k := by omega
  have h_BTField_eq : BTField k = BTField (k-1+1) := by
    apply BTField.cast_BTField_eq
    exact h_eq.symm
  have h_unique := unique_linear_decomposition_succ (k:=(k-1)) (x:=(Eq.mp (h:=h_BTField_eq) x))
  exact h_unique.choose

/-! Proofs that `split` is the inverse of `join_via_add_smul`
-/
theorem eq_join_via_add_smul_eq_iff_split (k : ‚Ñï) (h_pos : k > 0)
    (x : BTField k) (hi_btf lo_btf : BTField (k - 1)) :
    x = ‚ãò hi_btf, lo_btf ‚ãô ‚Üî
  split (k:=k) (h_k:=h_pos) x = (hi_btf, lo_btf) := by
  have h_k_sub_1_add_1_eq_k : k - 1 + 1 = k := by omega
  have h_BTField_eq := BTField.cast_BTField_eq (k:=k) (m:=k-1+1) (h_eq:=by omega)
  set p := unique_linear_decomposition_succ (k:=(k-1)) (x:=(Eq.mp (h:=h_BTField_eq) x)) with hp
  -- -- ‚ä¢ x = join_via_add_smul k h_pos hi lo
  have h_p_satisfy := p.choose_spec
  set xPair := p.choose
  constructor
  ¬∑ intro h_x_eq_join
    -- Due to `unique_linear_decomposition_succ`, there must be exactly one pair
    -- `(hi, lo)` that satisfies the equation : `x = join_via_add_smul k h_pos hi lo`
    -- Now we prove `‚ü®hi_btf, lo_btf‚ü©` is `Exists.choose` of `unique_linear_decomposition_succ`
    -- which is actually same as the definition of the `split` function
    have h_must_eq := h_p_satisfy.2 (‚ü®hi_btf, lo_btf‚ü©)
    simp only [eq_mp_eq_cast] at h_must_eq
    have h_hibtf_lobtf_eq_xPair := h_must_eq (by
      rw! (castMode := .all) [h_k_sub_1_add_1_eq_k]
      simp only [cast_eq]
      convert h_x_eq_join
      ¬∑ rw [eqRec_eq_cast]; rfl
      ¬∑ rw [eqRec_eq_cast]; rfl
    )
    have h_split_eq_xPair : split k h_pos x = xPair := by rfl
    rw [h_split_eq_xPair, h_hibtf_lobtf_eq_xPair.symm]
  ¬∑ intro h_split_eq
    unfold split at h_split_eq
    have h_hibtf_lobtf_eq_xPair : ‚ü®hi_btf, lo_btf‚ü© = xPair := by rw [‚Üêh_split_eq]
    have h_xPair_satisfy_join_via_add_smul := h_p_satisfy.1
    rw [‚Üêh_hibtf_lobtf_eq_xPair] at h_xPair_satisfy_join_via_add_smul
    rw [eq_mp_eq_cast] at h_xPair_satisfy_join_via_add_smul
    rw! (castMode := .all) [h_k_sub_1_add_1_eq_k] at h_xPair_satisfy_join_via_add_smul
    simp only [cast_eq] at h_xPair_satisfy_join_via_add_smul
    convert h_xPair_satisfy_join_via_add_smul
    ¬∑ rw [eqRec_eq_cast]; rfl
    ¬∑ rw [eqRec_eq_cast]; rfl

lemma split_join_via_add_smul_eq_iff_split (k : ‚Ñï) (h_pos : k > 0)
    (hi_btf lo_btf : BTField (k - 1)) :
    split (k:=k) (h_k:=h_pos) (‚ãò hi_btf, lo_btf ‚ãô) = (hi_btf, lo_btf) := by
  exact (eq_join_via_add_smul_eq_iff_split k h_pos (‚ãò hi_btf, lo_btf ‚ãô) hi_btf lo_btf).mp rfl

lemma join_eq_join_iff (k : ‚Ñï) (h_pos : k > 0) (hi‚ÇÅ hi‚ÇÄ lo‚ÇÅ lo‚ÇÄ : BTField (k - 1)) :
    ‚ãò hi‚ÇÅ, lo‚ÇÅ ‚ãô = ‚ãò hi‚ÇÄ, lo‚ÇÄ ‚ãô ‚Üî
  hi‚ÇÅ = hi‚ÇÄ ‚àß lo‚ÇÅ = lo‚ÇÄ := by
  constructor
  ¬∑ intro h_join_eq
    rw [eq_join_via_add_smul_eq_iff_split] at h_join_eq
    rw [split_join_via_add_smul_eq_iff_split] at h_join_eq
    simp only [Prod.mk.injEq] at h_join_eq
    exact h_join_eq
  ¬∑ intro h_hi_eq_lo_eq
    simp only [h_hi_eq_lo_eq]

/--
An element `x` lifted from the base field `BTField (k-1)` has `(0, x)` as its
split representation in `BTField k`.
-/
lemma split_algebraMap_eq_zero_x {k : ‚Ñï} (h_pos : k > 0) (x : BTField (k - 1)) :
  letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  split (k:=k) (h_k:=h_pos) (algebraMap (BTField (k - 1)) (BTField k) x) = (0, x) := by
  -- this one is long because of the `cast` stuff, but it should be quite straightforward
  -- via def of `canonicalEmbedding` and `eq_join_via_add_smul_eq_iff_split`
  letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  set mappedVal := algebraMap (BTField (k - 1)) (BTField k) x
  -- ‚ä¢ split k h_pos mappedVal = (0, x)
  have h := eq_join_via_add_smul_eq_iff_split (k:=k) (h_pos:=by omega)
    (x:=mappedVal) (hi_btf:=0) (lo_btf:=x)
  apply h.mp
  -- ‚ä¢ mappedVal = join_via_add_smul h_pos 0 x
  unfold mappedVal
  rw [algebraMap, Algebra.algebraMap]
  unfold instAlgebra binaryAlgebraTower
  rw [AlgebraTower.toAlgebra, AlgebraTower.algebraMap, instAlgebraTowerNatBTField]
  simp only
  have h_concrete_embedding_succ_1 := towerAlgebraMap_succ_1 (k:=k-1)
  rw! (castMode:=.all) [Nat.sub_one_add_one (by omega)] at h_concrete_embedding_succ_1
  rw! (castMode:=.all) [h_concrete_embedding_succ_1]
  rw [eqRec_eq_cast]
  rw [BTField.RingHom_cast_dest_apply (f:=canonicalEmbedding (k - 1))
    (x:=x) (h_eq:=by omega)]
  -- ‚ä¢ cast ‚ãØ ((canonicalEmbedding (k - 1)) x) = join_via_add_smul h_pos 0 x
  have h_k_sub_1_add_1 : k - 1 + 1 = k := by omega
  conv_lhs => enter [2]; rw! (castMode:=.all) [h_k_sub_1_add_1]; simp only
  rw [eqRec_eq_cast, eqRec_eq_cast, cast_cast, cast_eq]
  unfold join_via_add_smul
  -- letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  rw [Algebra.smul_def', map_zero, zero_mul, zero_add]
  have h := algebraMap_adjacent_tower_def (l:=k-1)
  rw! (castMode:=.all) [Nat.sub_one_add_one (by omega)] at h
  simp only [algebra_adjacent_tower, eqRec_eq_cast] at h
  rw [algebraMap, Algebra.algebraMap] at ‚ä¢ h
  rw! (castMode:=.all) [Nat.sub_one_add_one (by omega)] at h
  simp only [cast_eq] at h
  conv_rhs => rw [h]
  rw [eqRec_eq_cast]

lemma algebraMap_succ_eq_zero_x {k : ‚Ñï} (h_pos : k > 0) (x : BTField (k - 1)) :
  letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  algebraMap (BTField (k - 1)) (BTField k) x = ‚ãò 0, x ‚ãô := by
  letI instAlgebra := binaryAlgebraTower (l:=k-1) (r:=k) (h_le:=by omega)
  have h := eq_join_via_add_smul_eq_iff_split (k:=k) (h_pos:=h_pos)
    (x:=(algebraMap (BTField (k - 1)) (BTField k)) x) (hi_btf:=0) (lo_btf:=x).mpr
  apply h
  exact split_algebraMap_eq_zero_x h_pos x

lemma algebraMap_eq_zero_x {i j : ‚Ñï} (h_le : i < j) (x : BTField i) :
    letI instAlgebra := binaryAlgebraTower (l:=i) (r:=j) (h_le:=by omega)
    letI instAlgebraPred := binaryAlgebraTower (l:=i) (r:=j-1) (h_le:=by omega)
    algebraMap (BTField i) (BTField j) x
      = ‚ãò 0, algebraMap (BTField i) (BTField (j-1)) x ‚ãô := by
  set d := j - i with d_eq
  induction hd : d with
  | zero =>
    have h_i_eq_j : i = j := by omega
    have h_i_ne_j : i ‚â† j := by omega
    contradiction
  | succ d' => -- this one does not even use inductive hypothesis
    have h_j_eq : j = i + d' + 1 := by omega
    change (towerAlgebraMap (l:=i) (r:=j) (h_le:=by omega)) x =
      join_via_add_smul (h_pos:=by omega) 0 ((towerAlgebraMap (l:=i) (r:=j-1) (h_le:=by omega)) x)
    rw! [h_j_eq]
    rw [towerAlgebraMap_succ (l:=i) (r:=i+d') (h_le:=by omega)]
    simp only [RingHom.coe_comp, Function.comp_apply, Nat.add_one_sub_one]
    set r := towerAlgebraMap (l:=i) (r:=i+d') (h_le:=by omega) x with h_r
    have h := algebraMap_succ_eq_zero_x (k:=i+d'+1) (h_pos:=by omega) r
    simp only [Nat.add_one_sub_one] at h
    rw [‚Üêh]
    rfl

@[simp]
theorem minPoly_of_powerBasisSucc_generator (k : ‚Ñï) :
  (minpoly (BTField k) (powerBasisSucc k).gen) = X^2 + (Z k) ‚Ä¢ X + 1 := by
  have h_minPoly := AdjoinRoot.minpoly_powerBasis_gen_of_monic (f:=poly k)
    (hf:=by exact polyMonic k)
  conv_rhs at h_minPoly => rw [poly_form, ‚Üêadd_assoc, ‚ÜêPolynomial.smul_eq_C_mul]
  rw [‚Üêh_minPoly]
  -- ‚ä¢ minpoly (BTField k) (powerBasisSucc k).gen = minpoly (BTField k) (powerBasis ‚ãØ).gen
  unfold powerBasisSucc
  simp only [PowerBasis.map_gen, powerBasis_gen, minpoly.algEquiv_eq]

def hli_level_diff_0 (l : ‚Ñï) :
  letI instAlgebra:= binaryAlgebraTower (l:=l) (r:=l) (h_le:=by omega)
  @Basis (Fin 1) (BTField l) (BTField l) _ _ instAlgebra.toModule:= by
  letI instAlgebra:= binaryAlgebraTower (l:=l) (r:=l) (h_le:=by omega)
  letI instModule:= instAlgebra.toModule
  apply @Basis.mk (Œπ:=Fin 1) (R:=BTField l) (M:=BTField l) _ _ instAlgebra.toModule (v:=fun _ => 1)
  ¬∑ -- This proof now works smoothly.
    rw [Fintype.linearIndependent_iff (R:=BTField l) (v:=fun (_ : Fin 1) => (1 : BTField l))]
    intro g hg j
    -- ‚ä¢ g i = 0
    unfold instModule at *
    unfold instAlgebra at *
    rw [binaryTowerAlgebra_id (by omega)] at *
    have hj : j = 0 := by omega
    simp only [Finset.univ_unique, Fin.default_eq_zero, Fin.isValue,
      smul_eq_mul, Finset.sum_singleton] at hg -- hg : g 0 = 0 ‚à® 1 = 0
    have h_one_ne_zero : (1 : BTField l) ‚â† (0 : BTField l) := by
      exact BTFieldNeZero1 (k:=l).out
    simp only [BTField, BTFieldIsField, Fin.isValue] at hg
    rw [Subsingleton.elim j 0] -- j must be 0
    rw [hg.symm]
    exact Eq.symm (MulOneClass.mul_one (g 0))
  ¬∑ rw [Set.range_const]
    have h : instAlgebra = Algebra.id (BTField l) := by
      unfold instAlgebra
      rw [binaryTowerAlgebra_id (by omega)]
    rw! [h] -- convert to Algebra.id for clear goal
    rw [Ideal.submodule_span_eq]
    rw [Ideal.span_singleton_one]

def BTField.isScalarTower_succ_right (l r : ‚Ñï) (h_le : l ‚â§ r) :=
  instAlgebraTowerNatBTField.toIsScalarTower (i:=l) (j:=r) (k:=r+1)
  (h1:=by omega) (h2:=by omega)

/--
The multilinear basis for `BTField œÑ` over `BTField k` is the set of multilinear monomials
in the tower generators `Z(k+1), ..., Z(œÑ)`.
This is done via scalar tower multiplication of power basis across adjacent levels.
-/
def multilinearBasis (l r : ‚Ñï) (h_le : l ‚â§ r) :
    letI instAlgebra : Algebra (BTField l) (BTField r) := binaryAlgebraTower (h_le:=h_le)
    Basis (Fin (2 ^ (r - l))) (BTField l) (BTField r) := by
  letI instAlgebra : Algebra (BTField l) (BTField r) := binaryAlgebraTower (h_le:=h_le)
  if h_r_sub_l : r - l = 0 then -- Avoid using `match` to avoid `Eq.rec` when reasoning recursively
    have h_l_eq_r : l = r := by omega
    subst h_l_eq_r
    have h_res := hli_level_diff_0 (l:=l)
    rw [‚ÜêNat.pow_zero 2, ‚ÜêNat.sub_self l] at h_res
    exact h_res
  else
    have h_l_lt_r : l < r := by omega
    set n' := r - l - 1 with h_n'
    set r1 := l + n' with h_r1
    have h_r_sub_l : r - l = n' + 1 := by omega
    have h_r1_sub_l : r1 - l = n' := by omega
    have h_r : r = r1 + 1 := by omega
    letI instAlgebraPrev : Algebra (BTField l) (BTField (r1)) :=
      binaryAlgebraTower (l:=l) (r:=r1) (h_le:=by omega)
    set prevMultilinearBasis : Basis (Fin (2 ^ (r1 - l))) (BTField l) (BTField r1)
      := multilinearBasis (l:=l) (r:=r1) (h_le:=by omega)
    rw! [h_r1_sub_l] at prevMultilinearBasis
    letI instAlgebra : Algebra (BTField l) (BTField (r1 + 1)) :=
      binaryAlgebraTower (l:=l) (r:=r1 + 1) (h_le:=by omega)
    rw! [h_r_sub_l]
    apply Basis.reindex (e:=revFinProdFinEquiv (m:=2^(n')) (n:=2)
      (h_m:=by exact Nat.two_pow_pos n'))
    -- ‚ä¢ Basis (Fin 2 √ó Fin (2 ^ n')) (BTField l) (BTField (r))
    have h_eq : l + (n' + 1) = (r1) + 1 := by rw [‚Üêadd_assoc]
    letI instAlgebraSucc : Algebra (BTField (r1)) (BTField (r1 + 1)) := by
      exact algebra_adjacent_tower (r1)
    letI instModuleSucc : Module (BTField l) (BTField (r1 + 1)) := by
      exact instAlgebra.toModule
    letI : IsScalarTower (BTField l) (BTField (r1)) (BTField (r1 + 1)) := by
      exact BTField.isScalarTower_succ_right (l:=l) (r:=r1) (h_le:=by omega)
    have res := Basis.smulTower (Œπ:=Fin (2 ^ n')) (Œπ':=Fin (2)) (R:=BTField l)
      (S:=BTField (r1)) (A:=BTField (r1 + 1))
      (b:=by
        convert prevMultilinearBasis;
      ) (c:=by
        convert (powerBasisSucc (r1)).basis
        rw [powerBasisSucc_dim (k:=r1)]
      )
    convert res
    -- Basis are equal under the same @binaryAlgebraTower
    -- ‚ä¢ Basis (Fin (2 ^ n') √ó Fin 2) (BTField l) (BTField r)
    -- = Basis (Fin (2 ^ n') √ó Fin 2) (BTField l) (BTField (r1 + 1))
    unfold instModuleSucc -- Module used in rhs
    rw! [h_r]

@[simp]
theorem BTField.PowerBasis.dim_of_eq_rec
    (r1 r : ‚Ñï)
    (h_r : r = r1 + 1)
    (b : PowerBasis (BTField r1) (BTField (r1 + 1))) :
    letI instAlgebra : Algebra (BTField r1) (BTField r) :=
      binaryAlgebraTower (l:=r1) (r:=r) (h_le:=by omega)
    ((Eq.rec (motive:=fun (x : ‚Ñï) (_ : r1 + 1 = x) => by
      letI instAlgebraCur : Algebra (BTField r1) (BTField x) :=
        binaryAlgebraTower (l:=r1) (r:=x) (h_le:=by omega)
      exact PowerBasis (BTField r1) (BTField x)) (refl:=b) (t:=h_r.symm)) :
        PowerBasis (BTField r1) (BTField r)).dim
    = b.dim := by
  subst h_r
  rfl

@[simp]
theorem PowerBasis.cast_basis_succ_of_eq_rec_apply
    (r1 r : ‚Ñï) (h_r : r = r1 + 1)
    (k : Fin 2) :
    letI instAlgebra : Algebra (BTField r1) (BTField r) :=
      binaryAlgebraTower (l:=r1) (r:=r) (h_le:=by omega)
    letI instAlgebraSucc : Algebra (BTField (r1 + 1)) (BTField (r)) :=
      binaryAlgebraTower (l:=r1 + 1) (r:=r) (h_le:=by omega)
    let b : PowerBasis (BTField r1) (BTField (r1 + 1)) := powerBasisSucc (k:=r1)
    let bCast : PowerBasis (BTField r1) (BTField r) := Eq.rec (motive:=
      fun (x : ‚Ñï) (_ : r1 + 1 = x) => by
        letI instAlgebraCur : Algebra (BTField r1) (BTField x) :=
          binaryAlgebraTower (l:=r1) (r:=x) (h_le:=by omega)
        exact PowerBasis (BTField r1) (BTField x)) (refl:=b) (t:=h_r.symm)
    have h_pb_dim : b.dim = 2 := by
      exact powerBasisSucc_dim r1

    have h_pb'_dim : bCast.dim = 2 := by
      dsimp [bCast]
      rw [BTField.PowerBasis.dim_of_eq_rec (r1:=r1) (r:=r) (h_r:=h_r) (b:=b)]
      exact h_pb_dim

    have h_pb_type_eq : Basis (Fin bCast.dim) (BTField r1) (BTField r) =
      Basis (Fin 2) (BTField r1) (BTField r) := by
      congr

   -- The `cast` needs a proof that `bCast.dim = 2`. We construct it here.
    let left : Basis (Fin 2) (BTField r1) (BTField r) := cast (by exact h_pb_type_eq) bCast.basis
    let right := (algebraMap (BTField (r1 + 1)) (BTField r))
      (b.basis (Fin.cast h_pb_dim.symm k))
    left k = right := by
  -- The proof of the theorem itself remains simple.
  subst h_r
  simp only [binaryTowerAlgebra_id,
    Algebra.algebraMap_self, PowerBasis.coe_basis, Fin.coe_cast, RingHom.id_apply]
  rw [BTField.Basis_cast_index_apply (h_eq:=by exact powerBasisSucc_dim r1) (h_le:=by omega)]
  simp only [PowerBasis.coe_basis, Fin.coe_cast]

/-!
The basis element at index `j` is the product of the tower generators at
the ON bits in binary representation of `j`.
-/
theorem multilinearBasis_apply (r : ‚Ñï) : ‚àÄ l : ‚Ñï, (h_le : l ‚â§ r) ‚Üí ‚àÄ (j : Fin (2  ^ (r - l))),
  multilinearBasis (l:=l) (r:=r) (h_le:=h_le) j =
    (Finset.univ : Finset (Fin (r - l))).prod (fun i =>
      (binaryAlgebraTower (l:=l + i + 1) (r:=r) (h_le:=by omega)).algebraMap (
        (ùïè (l + i)) ^ (Nat.getBit i j))) := by
  -- letI instAlgebra : Algebra (BTField l) (BTField r) := binaryAlgebraTower (h_le:=h_le)
  induction r with
  | zero => -- Fin (2^0) = Fin 1, so j = 0
    intro l h_l_le_0 j
    simp only [zero_tsub, pow_zero] at j
    have h_l_eq_r : l = 0 := by omega
    subst h_l_eq_r
    simp only [Nat.sub_zero, Nat.pow_zero, Finset.univ_eq_empty,
      ùïè, Z, Inhabited, list, Fin.val_eq_zero, Finset.prod_empty]
    have hj_eq_0 : j = 0 := by exact Fin.eq_of_val_eq (by omega)
    rw! [hj_eq_0]
    rw [multilinearBasis]
    simp only [tsub_self, ‚ÜìreduceDIte, Nat.sub_zero, Nat.pow_zero, Fin.isValue]
    rw [hli_level_diff_0]
    simp only [eq_mp_eq_cast, cast_eq, Fin.isValue, Basis.coe_mk]
  | succ r1 ih_r1 =>
    set r := r1 + 1 with hr
    intro l h_l_le_r j
    haveI instAlgebraR : Algebra (BTField r) (BTField r) :=
      binaryAlgebraTower (l:=r) (r:=r) (h_le:=by omega)
    haveI instModuleR : Module (BTField r) (BTField r) := instAlgebraR.toModule
    if h_r_sub_l : r - l = 0 then
      rw [multilinearBasis]
      have h_l_eq_r : l = r := by omega
      subst h_l_eq_r
      simp only [tsub_self, ‚ÜìreduceDIte, Nat.pow_zero,
        hli_level_diff_0, eq_mp_eq_cast, cast_eq]
      have h1 : 1 = 2 ^ (r - r) := by rw [Nat.sub_self, Nat.pow_zero];
      have h_r_sub_r : r - r = 0 := by omega
      rw [‚ÜêFin.prod_congr' (b:=r-r) (a:=0) (h:=by omega), Fin.prod_univ_zero]
      rw [BTField.Basis_cast_index_apply (h_eq:=by omega) (h_le:=by omega)]
      simp only [Basis.coe_mk]
    else
      rw [multilinearBasis]
      -- key to remove Eq.rec : dif_neg h_r_sub_l
      simp only [Nat.pow_zero, eq_mp_eq_cast, cast_eq,
        eq_mpr_eq_cast, dif_neg h_r_sub_l]
      have h2 : 2 ^ (r - l - 1) * 2 = 2 ^ (r - l) := by
        rw [‚ÜêNat.pow_succ, Nat.succ_eq_add_one, Nat.sub_add_cancel (by omega)]
      rw [BTField.Basis_cast_index_apply (h_eq:=by omega) (h_le:=by omega)]
      simp only [Basis.coe_reindex, Function.comp_apply,
        revFinProdFinEquiv_symm_apply]
      rw [BTField.Basis_cast_dest_apply (h_eq:=by omega) (h_le1:=by omega) (h_le2:=by omega)]

      set prevDiff := r - l - 1 with h_prevDiff
      have h_r_sub_l : r - l = prevDiff + 1 := by omega
      have h_r1_sub_l : r1 - l = prevDiff := by omega
      have h_r1_eq_l_plus_prevDiff : r1 = l + prevDiff := by omega
      have h_r : r = r1 + 1 := by omega
      have h1 : l + (r - l - 1) = r1 := by omega
      letI instAlgebraPrev : Algebra (BTField l) (BTField (r1)) :=
        binaryAlgebraTower (l:=l) (r:=r1) (h_le:=by omega)
      set prevMultilinearBasis : Basis (Fin (2 ^ (r1 - l))) (BTField l) (BTField r1) :=
        multilinearBasis (l:=l) (r:=r1) (h_le:=by omega) with h_prevMultilinearBasis
      rw! [h_r1_sub_l] at prevMultilinearBasis
      letI instAlgebra : Algebra (BTField l) (BTField (r1 + 1)) :=
        binaryAlgebraTower (l:=l) (r:=r1 + 1) (h_le:=by omega)
      rw! (castMode:=.all) [h1]

      letI instAlgebraSucc : Algebra (BTField (r1)) (BTField (r1 + 1)) := by
        exact algebra_adjacent_tower (r1)
      letI instModuleSucc : Module (BTField l) (BTField (r1 + 1)) := by
        exact instAlgebra.toModule

      letI : IsScalarTower (BTField l) (BTField (r1)) (BTField (r1 + 1)) := by
        exact BTField.isScalarTower_succ_right (l:=l) (r:=r1) (h_le:=by omega)
      rw [Basis.smulTower_apply]
      rw [Algebra.smul_def]
      rw [BTField.cast_mul (m:=r1 + 1) (n:=r) (h_eq:=by omega)]
      rw! (castMode:=.all) [h_r.symm]
      rw [cast_eq, cast_eq]

      letI instAlgebra2 : Algebra (BTField r1) (BTField r) :=
        binaryAlgebraTower (l:=r1) (r:=r) (h_le:=by omega)
      letI instModule2 : Module (BTField r1) (BTField r) := instAlgebra2.toModule
      set b := (powerBasisSucc r1) with hb
      rw! [‚Üêhb]
      simp_rw [eqRec_eq_cast]
      rw [cast_eq]
      have h : (2 ^ (r1 - l)) = (2 ^ (r - l - 1)) := by
        rw [h_r]
        rw [Nat.sub_right_comm, Nat.add_sub_cancel r1 1]
      rw [BTField.Basis_cast_index_apply (h_eq:=h) (h_le:=by omega)]
      simp only [leftDivNat, Fin.coe_cast]

      set indexLeft : Fin 2 := ‚ü®j.val / 2 ^ (r - l - 1), by
        change j.val / 2 ^ (r - l - 1) < 2^1
        apply div_two_pow_lt_two_pow (x:=j.val) (i:=1) (j:=r-l-1) (h_x_lt_2_pow_i:=by
          rw [Nat.add_comm, Nat.sub_add_cancel (by omega)];
          exact j.isLt
        )
      ‚ü©

      have h_cast_basis_succ_of_eq_rec_apply :=
        PowerBasis.cast_basis_succ_of_eq_rec_apply (r1:=r1) (r:=r) (h_r:=h_r) (k:=indexLeft)
      simp only at h_cast_basis_succ_of_eq_rec_apply
      -- ‚ä¢ .. (cast ‚ãØ (‚ãØ ‚ñ∏ b).basis) indexLeft = ‚àè i, algebraMap (ùïè (l + ‚Üëi) ^ bit ‚Üëi ‚Üëj)
      unfold algebra_adjacent_tower
      -- Now make instance in (cast ‚ãØ (‚ãØ ‚ñ∏ b).basis) uses (r+1) instead of r, so it's compatible
      -- with h_cast_basis_succ_of_eq_rec_apply
      rw! (castMode:=.all) [‚Üêh_r]
      simp only;
      conv =>
        lhs
        arg 2
        rw! (castMode:=.all) [h_cast_basis_succ_of_eq_rec_apply]

      unfold indexLeft
      -- All casts eliminated, now we prove equality on revFinProdFinEquiv and bit stuff
      -- ‚ä¢ (algebraMap (BTField r1) (BTField r)) (prevMultilinearBasis‚úù
      -- (Fin.cast ‚ãØ (leftModNat ‚ãØ (Fin.cast ‚ãØ j)))) * (algebraMap (BTField (r1 + 1)) (BTField r))
      -- ((powerBasisSucc r1).basis (Fin.cast ‚ãØ ‚ü®‚Üëj / 2 ^ (r - l - 1), ‚ãØ‚ü©)) =
      --   ‚àè i, Algebra.algebraMap (ùïè (l + ‚Üëi) ^ bit ‚Üëi ‚Üëj)
      conv_lhs =>
        simp only [Fin.cast_mk, PowerBasis.coe_basis];
        rw [powerBasisSucc_gen, ‚Üêùïè] -- convert to gen^i form
        rw [ih_r1 (l:=l) (h_le:=by omega)] -- inductive hypothesis of level r - 1
        rw [Fin.cast_val_eq_val (h_eq:=by omega)]

      conv_rhs =>
        rw [‚ÜêFin.prod_congr' (b:=r-l) (a:=prevDiff + 1) (h:=by omega)]
        rw [Fin.prod_univ_castSucc] -- split the prod of rhs
        simp only [Fin.coe_cast, Fin.coe_castSucc, Fin.val_last]

      simp_rw [algebraMap.coe_pow] -- rhs
      simp_rw [algebraMap.coe_prod] -- lhs
      unfold Algebra.cast
      rw! (castMode:=.all) [‚ÜêalgebraMap]
      conv_lhs =>
        rw [‚ÜêFin.prod_congr' (b:=r1-l) (a:=prevDiff) (h:=by omega)]
        simp only [Fin.coe_cast]
      simp_rw [algebraMap, instAlgebraSucc, algebra_adjacent_tower]
      rw [RingHom.map_pow]
      simp_rw [‚ÜêbinaryTowerAlgebra_apply_assoc]
      ------------------ Equality of bit-based powers of generators -----------------
      --- The outtermost term
      have hfinProd_msb := bit_revFinProdFinEquiv_symm_2_pow_succ (n:=prevDiff)
        (i:=‚ü®prevDiff, by omega‚ü©) (j:=‚ü®j, by omega‚ü©)
      simp only [lt_self_iff_false, ‚ÜìreduceIte, revFinProdFinEquiv_symm_apply] at hfinProd_msb
      conv_rhs =>
        simp only [hfinProd_msb, leftDivNat];
        rw! [h_r1_eq_l_plus_prevDiff.symm];
        simp only [h_prevDiff]
      --- Inner-prod term
      congr
      funext i
      have hfinProd_lsb := bit_revFinProdFinEquiv_symm_2_pow_succ (n:=prevDiff) (i:=‚ü®i, by omega‚ü©)
        (j:=‚ü®j, by omega‚ü©)
      simp only [Fin.is_lt, ‚ÜìreduceIte, revFinProdFinEquiv_symm_apply] at hfinProd_lsb
      rw [hfinProd_lsb]
      rfl

end MultilinearBasis
end
